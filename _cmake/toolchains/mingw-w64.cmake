if(DEFINED __MINGW_TOOLCHAIN_INCLUDED)
    return()
endif()
set(__MINGW_TOOLCHAIN_INCLUDED TRUE)

set(default_mingw_triplet "x86_64-w64-mingw32")
set(default_mingw_prefix "/usr/${default_mingw_triplet}")

set(MINGW_TRIPLET "$ENV{MINGW_TRIPLET}")
if(NOT MINGW_TRIPLET)
    set(MINGW_TRIPLET "${default_mingw_triplet}")
endif()

set(MINGW_PREFIX "$ENV{MINGW_PREFIX}")
if(NOT MINGW_PREFIX)
    set(MINGW_PREFIX "${default_mingw_prefix}")
endif()

set(CMAKE_SYSTEM_NAME Windows)
set(CMAKE_SYSTEM_PROCESSOR x86_64)
set(CMAKE_EXECUTABLE_SUFFIX ".exe")

find_program(MINGW_C_COMPILER "${MINGW_TRIPLET}-gcc" REQUIRED)
find_program(MINGW_CXX_COMPILER "${MINGW_TRIPLET}-g++" REQUIRED)
find_program(MINGW_RC_COMPILER "${MINGW_TRIPLET}-windres")

set(CMAKE_C_COMPILER "${MINGW_C_COMPILER}" CACHE FILEPATH "" FORCE)
set(CMAKE_CXX_COMPILER "${MINGW_CXX_COMPILER}" CACHE FILEPATH "" FORCE)
if(MINGW_RC_COMPILER)
    set(CMAKE_RC_COMPILER "${MINGW_RC_COMPILER}" CACHE FILEPATH "" FORCE)
endif()

set(CMAKE_FIND_ROOT_PATH "${MINGW_PREFIX}")

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

message(STATUS "Using MinGW toolchain: ${MINGW_TRIPLET} (prefix: ${MINGW_PREFIX})")

